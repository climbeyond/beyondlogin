import org.jetbrains.kotlin.gradle.plugin.mpp.apple.XCFramework

plugins {
    `maven-publish`

    alias(libs.plugins.kotlinMultiplatform)
    id("com.android.kotlin.multiplatform.library")

    alias(libs.plugins.serialization)
    alias(libs.plugins.jetbrainsCompose)
    alias(libs.plugins.compose.compiler)
}

kotlin {
    jvmToolchain(21)

    android {
        namespace = "app.climbeyond.beyondlogin"
        compileSdk = rootProject.ext.get("androidCompileSdk") as Int
        minSdk = rootProject.extra.get("androidMinSdk") as Int
    }

    val xcf = XCFramework("BeyondLogin")
    listOf(
        iosArm64(),
        iosSimulatorArm64()
    ).forEach {
        it.binaries.framework {
            baseName = "BeyondLogin"
            xcf.add(this)
            isStatic = true
            binaryOption("bundleId","app.climbeyond.beyondlogin.ios")
        }
    }

    sourceSets {
        all {
            languageSettings {
                optIn("org.jetbrains.compose.resources.ExperimentalResourceApi")
                optIn("kotlin.time.ExperimentalTime")
            }
        }

        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material)
            @OptIn(org.jetbrains.compose.ExperimentalComposeLibrary::class)
            implementation(compose.components.resources)

            implementation(libs.kotlinx.datetime)
            implementation(libs.kotlin.serialization)
            implementation(libs.kotlinx.serialization.json)

            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.logging)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.client.serialization)
            implementation(libs.ktor.serialization.kotlinx.json)
        }

        androidMain.dependencies {
            implementation(libs.androidx.preference.ktx)
        }

        iosMain.dependencies {
            implementation(libs.ktor.client.ios)
        }
    }

    targets.all {
        compilations.all {
            compileTaskProvider.configure {
                compilerOptions.freeCompilerArgs.add("-Xexpect-actual-classes")
            }
        }
    }
}

publishing {
    val githubUser: String? by project
    val githubToken: String? by project

    repositories {
        version = rootProject.ext.get("versionName") as String
        maven {
            setUrl("https://maven.pkg.github.com/climbeyond/beyondlogin")
            credentials {
                username = githubUser
                password = githubToken
            }
        }
    }
}

afterEvaluate {
    tasks.getByName("linkDebugFrameworkIosArm64") {
        onlyIf { return@onlyIf false }
    }
    tasks.getByName("linkDebugFrameworkIosSimulatorArm64") {
        onlyIf { return@onlyIf false }
    }
}

tasks.register<Copy>("publish-android") {
    val androidName = "beyondlogin-${rootProject.extra.get("versionName") as String}.aar"
    val apkDir = file("${project.rootDir.absolutePath}/shared/build/outputs/aar/beyondlogin-release.aar")
    val outDir = file("${project.rootDir.absolutePath}/aar")

    from(apkDir)
    into(outDir)
    include("**/*")
    rename("beyondlogin-release.aar", androidName)
    doLast {
        println(">>>publish $androidName success!" +
                "\nfrom: $apkDir" +
                "\ninto: $outDir")
    }
}

tasks.register<Copy>("publish-ios") {
    // iOS xcFramework copy
    val xcSaveDir = "beyondlogin-${rootProject.extra.get("versionName") as String}.xcframework"
    val xcDir = file("${project.rootDir.absolutePath}/shared/build/XCFrameworks/release/BeyondLogin.xcframework")
    val outXcDir = file("${project.rootDir.absolutePath}/xcframework/${xcSaveDir}")

    from(xcDir)
    into(outXcDir)
    include("**/*")
    doLast {
        println(">>>publish $xcSaveDir success!" +
                "\nfrom: $xcDir" +
                "\ninto: $outXcDir")
    }
}
